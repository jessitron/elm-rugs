@tag "elm"
@description "General editor for adding subscriptions"
editor Subscribe

@description "The library that provides the subscription"
param library_name: ^.*$

param library_version: ^.*$

@description "Package that contains the function that gives us a subscription"
param namespace: ^.*$

@description "Function to call that gives us a subscription"
param name_of_subscription: ^.*$

@description "Message to pass around to respond to subscription"
param message: ^.*$

@description "Type of data the subscription provides"
param type_of_content: ^.*$

@description "What to call the model field that holds the data we get from the subscription"
param name_of_content: ^.*$

AddDependency dependency_name=library_name, dependency_version=library_version

AddImport module="Main", module_to_import=namespace

AddToModel field_name=name_of_content, field_type={ "Maybe " + type_of_content }, initial_value="Nothing"

AddMessage constructor={ message + " " + type_of_content },
           deconstructor={ message + " " + name_of_content },
           update_model={ return ('{ model | ' + name_of_content + " = Just " + name_of_content + ' }') }

AddSubscription subscription={ return (namespace + "." + name_of_subscription + " " + message) }
